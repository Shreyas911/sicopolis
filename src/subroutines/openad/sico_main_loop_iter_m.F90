!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!
!  Module : s i c o _ m a i n _ l o o p _ i t e r _ m 
!
!> @file
!!
!! A catch-all module for openad-related subroutines.
!!
!! @section Copyright
!!
!! Copyright 2017-2022 Liz Curry-Logan, Sri Hari Krishna Narayanan
!!
!! @section License
!!
!! This file is part of SICOPOLIS.
!!
!! SICOPOLIS is free software: you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation, either version 3 of the License, or
!! (at your option) any later version.
!!
!! SICOPOLIS is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with SICOPOLIS.  If not, see <http://www.gnu.org/licenses/>.
!<
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

module sico_main_loop_iter_m

  implicit none

  public

contains

!-------------------------------------------------------------------------------
!> The interior of subroutine sico_main_loop. If new subroutines / modules are 
!! introduced into the main calculation, they must also be inserted here.  
!<------------------------------------------------------------------------------
  subroutine sico_main_loop_iter(delta_ts, glac_index, &
                      mean_accum, &
                      dtime, dtime_temp, dtime_wss, dtime_out, dtime_ser, &
                      time, time_init, time_end, time_output, &
                      dxi, deta, dzeta_c, dzeta_t, dzeta_r, &
                      z_mar, &
                      ndat2d, ndat3d, n_output, &
                      itercount,iter_temp,iter_wss,iter_ser,&
                      iter_out,iter_output)

  use sico_types_m
  use sico_variables_m
  use sico_vars_m
  use boundary_m
#if (defined(GRL) && DISC>0)
  use discharge_workers_m
#endif
#if (CALCMOD==0 || CALCMOD==1 || CALCMOD==-1)
  use calc_temp_m
#elif (CALCMOD==2 || CALCMOD==3)
  use calc_temp_enth_m
#endif

  use calc_enhance_m
  use flag_update_gf_gl_cf_m
  use calc_vxy_m
  use calc_vz_m
  use calc_dxyz_m
  use calc_gia_m
  use calc_thk_m
  use calc_temp_melt_bas_m
  use calc_bas_melt_m
  use calc_thk_water_bas_m
  use ctrl_m, only: myfloor

  implicit none

  integer(i4b),       intent(in)    :: n_output
  real(dp),           intent(in)    :: mean_accum
  real(dp),           intent(in)    :: dtime, dtime_temp, dtime_wss, &
                                       dtime_out, dtime_ser
  real(dp),           intent(in)    :: time_init, time_end, time_output(100)
  real(dp),           intent(in)    :: dxi, deta, dzeta_c, dzeta_t, dzeta_r

  integer(i4b),       intent(inout) :: ndat2d, ndat3d
  real(dp),           intent(inout) :: delta_ts, glac_index
  real(dp),           intent(inout) :: time
  real(dp),           intent(inout) :: z_mar

  integer(i4b)                      :: i, j, kc, kt, kr, n
  integer(i4b),       intent(inout) :: itercount, iter_temp, iter_wss 
  integer(i4b),       intent(inout) :: iter_ser, iter_out, iter_output(100)
  real(dp)                          :: dtime_temp_inv
  logical                           :: flag_3d_output

  real(dp)                          :: tmp, valmod
  integer(i4b)                      :: valmodint

  write(unit=6, fmt='(2x,i0)') itercount
  
  !-------- Update of time --------
  
  time      = time_init + real(itercount,dp)*dtime
  
  !-------- Save old mask --------
  
  mask_old = mask
  
  !-------- Boundary conditions --------
  
  call boundary(time, dtime, dxi, deta, delta_ts, glac_index, z_mar)
  
  !-------- Temperature, water content, age, flow enhancement factor --------
  if ((itercount - (iter_temp*int(real(itercount)/real(iter_temp)))) == 0) then 
 
     write(unit=6, fmt='(10x,a)') 'Computation of T'
  
  !  ------ Temperature, water content, age
  
#if (CALCMOD==1)
     call calc_temp_poly(dxi, deta, dzeta_c, dzeta_t, dzeta_r, dtime_temp)
                    ! polythermal scheme (POLY)
  
#elif (CALCMOD==0)
     call calc_temp_cold(dxi, deta, dzeta_c, dzeta_t, dzeta_r, dtime_temp)
                    ! cold-ice scheme (COLD)
  
#elif (CALCMOD==2 || CALCMOD==3)
     call calc_temp_enth(dxi, deta, dzeta_c, dzeta_t, dzeta_r, dtime_temp)
                    ! enthalpy scheme (ENTC or ENTM)
  
#elif (CALCMOD==-1)
     call calc_temp_const()   ! isothermal scheme (ISOT)
  
#else
     stop ' >>> sico_main_loop: Parameter CALCMOD must be between -1 and 3!'
#endif
  
  !  ------ Time derivative of H_t (further time derivatives are
  !         computed in subroutine calc_thk_xxx)
  
     dtime_temp_inv = 1.0_dp/dtime_temp
  
     dH_t_dtau      = (H_t_new - H_t)*dtime_temp_inv
  
  !  ------ New values --> old values
  
     n_cts   = n_cts_new
     kc_cts  = kc_cts_new
     zm      = zm_new
     H_c     = H_c_new
     H_t     = H_t_new
     temp_c  = temp_c_new
     age_c   = age_c_new
     omega_t = omega_t_new
     age_t   = age_t_new
     temp_r  = temp_r_new
  
#if (CALCMOD==2 || CALCMOD==3)
     enth_c  = enth_c_new
     enth_t  = enth_t_new
     omega_c = omega_c_new
#endif
  
  !  ------ Flow enhancement factor
  
#if (ENHMOD==1)
   call calc_enhance_1()
#elif (ENHMOD==2)
   call calc_enhance_2()
#elif (ENHMOD==3)
   call calc_enhance_3(time)
#elif (ENHMOD==4)
   call calc_enhance_4()
#elif (ENHMOD==5)
   call calc_enhance_5()
#else
     stop ' >>> sico_main_loop: Parameter ENHMOD must be between 1 and 5!'
#endif
  
  end if
  !     End of computation of temperature, water content, age and
  !     enhancement factor
  
  !-------- Velocity --------

  call flag_update_gf_gl_cf()
  
#if (DYNAMICS==1 || DYNAMICS==2)
  
  call calc_vxy_b_sia(time)
  call calc_vxy_sia(dzeta_c, dzeta_t)
  
#if (MARGIN==3 || DYNAMICS==2)
  call calc_vxy_ssa(dxi, deta, dzeta_c, dzeta_t)
#endif
  
  call calc_vz_grounded(dxi, deta, dzeta_c, dzeta_t)
  
#if (MARGIN==3)
  call calc_vz_floating(dxi, deta, dzeta_c)
#endif
  
#elif (DYNAMICS==0)
  
  call calc_vxy_static()
  call calc_vz_static()
  
#else
     stop ' >>> sico_main_loop: DYNAMICS must be either 0, 1 or 2!'
#endif
  
  call calc_dxyz(dxi, deta, dzeta_c, dzeta_t)
  
  !-------- Glacial isostatic adjustment and ice topography --------
  
  call calc_gia(time, dtime, dxi, deta, itercount, iter_wss)
  
  call calc_thk_init()
  
#if ((MARGIN==3 || DYNAMICS==2) && (CALCTHK==1 || CALCTHK==2 || CALCTHK==3))
  write(6, fmt='(a)') ' >>> sico_main_loop:'
  write(6, fmt='(a)') '          Non-SIA dynamics combined with'
  write(6, fmt='(a)') '          SIA ice thickness solver!'
  stop
#endif
  
#if (CALCTHK==1)
  call calc_thk_sia_expl(time, dtime, dxi, deta, z_mar)
#elif (CALCTHK==2 || CALCTHK==3)
  call calc_thk_sia_impl(time, dtime, dxi, deta, z_mar, mean_accum)
#elif (CALCTHK==4)
  call calc_thk_expl(time, dtime, dxi, deta, z_mar)
#elif (CALCTHK==5 || CALCTHK==6)
  call calc_thk_impl(time, dtime, dxi, deta, z_mar, mean_accum)
#else
  stop     ' >>> sico_main_loop: Parameter CALCTHK must be between 1 and 6!'
#endif
  
#if (MARGIN==3)       /* coupled SIA/SSA or SIA/SStA/SSA dynamics */
  call calc_thk_mask_update(time, dtime, dxi, deta, z_mar, 3)
#elif (DYNAMICS==2)   /* hybrid SIA/SStA dynamics */
  call calc_thk_mask_update(time, dtime, dxi, deta, z_mar, 2)
#else                 /* SIA-only dynamics */
#if (CALCTHK==1 || CALCTHK==2 || CALCTHK==3)
  call calc_thk_mask_update(time, dtime, dxi, deta, z_mar, 1)
#elif (CALCTHK==4 || CALCTHK==5 || CALCTHK==6)
  call calc_thk_mask_update(time, dtime, dxi, deta, z_mar, 2)
#endif
#endif
  
  !  ------ New values --> old values

  call flag_update_gf_gl_cf()
  
  zs = zs_new
  zm = zm_new
  zb = zb_new
  zl = zl_new
  H_c= H_c_new
  H_t= H_t_new
  
  !-------- Melting temperature --------
  
  call calc_temp_melt()
  
  !-------- Basal temperature --------
  
  call calc_temp_bas()
  
  !-------- Basal melting rate --------
  
  call calc_qbm(time, dzeta_c, dzeta_r)
  
  !-------- Effective thickness of subglacial water  --------
  
  call calc_thk_water_bas()
  
  !-------- Data output --------
  ! We do none of the original data output in adjoint mode

  end subroutine sico_main_loop_iter

end module sico_main_loop_iter_m
